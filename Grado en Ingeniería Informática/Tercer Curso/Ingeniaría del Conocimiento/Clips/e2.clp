(deftemplate oav-u
	(slot objeto (type SYMBOL))
	(slot atributo(type SYMBOL))
	(slot valor)
)

(deftemplate oav-m
	(slot objeto (type SYMBOL))
	(slot atributo(type SYMBOL))
	(slot valor)
)

(defrule semanticaUnivaluadaOAV
	?x<-(oav-u  (objeto ?objeto)
		(atributo ?atributo)
		(valor ?valor1))
	?y<-(oav-u  (objeto ?objeto)
		(atributo ?atributo)
		(valor ?valor2 & ~?valor1))
	(test(< (fact-index ?x) (fact-index ?y)))
	=>
	(retract ?x)
)


(deffacts hechosInicialesPaciente-1 
	(oav-u  (objeto paciente-1)
		(atributo nombre)
		(valor Carlos))
	(oav-u  (objeto paciente-1)
		(atributo paciente)
		(valor true))
	(oav-u  (objeto paciente-1)
		(atributo sexo)
		(valor hombre))
	(oav-u  (objeto paciente-1)
		(atributo edad)
		(valor 10))
	(oav-u  (objeto paciente-1)
		(atributo anosFumando)
		(valor 0))
	(oav-u  (objeto paciente-1)
		(atributo obeso)
		(valor false))
	(oav-m  (objeto paciente-1)
		(atributo sintomas)
		(valor dolorAbdominal))
	(oav-m  (objeto paciente-1)
		(atributo evidenciaClinica)
		(valor rumorDiastolico))
	(oav-u  (objeto paciente-1)
		(atributo presionSistolica)
		(valor 130))
	(oav-u  (objeto paciente-1)
		(atributo presionDiastolica)
		(valor 60)))
		
(deffacts hechosInicialesPaciente-2
	(oav-u  (objeto paciente-2)
		(atributo nombre)
		(valor Elisa))
	(oav-u  (objeto paciente-2)
		(atributo paciente)
		(valor true))
	(oav-u  (objeto paciente-2)
		(atributo sexo)
		(valor Mujer))
	(oav-u  (objeto paciente-2)
		(atributo edad)
		(valor 70))
	(oav-u  (objeto paciente-2)
		(atributo anosFumando)
		(valor 10))
	(oav-u  (objeto paciente-2)
		(atributo obeso)
		(valor false))
	(oav-m  (objeto paciente-2)
		(atributo sintomas)
		(valor dolorAbdominal))
	(oav-m  (objeto paciente-2)
		(atributo sintomas)
		(valor calambresPiernas))
	(oav-m  (objeto paciente-2)
		(atributo evidenciaClinica)
		(valor rumorAbdominal))
	(oav-m  (objeto paciente-2)
		(atributo evidenciaClinica)
		(valor masaPulsante))
	(oav-u  (objeto paciente-2)
		(atributo presionSistolica)
		(valor 150))
	(oav-u  (objeto paciente-2)
		(atributo presionDiastolica)
		(valor 95)))
(deffacts enfermedades
	(oav-u  (objeto enfermedadCardiovascular-1)
		(atributo nombre)
		(valor aneurismaAortico))
	(oav-u  (objeto enfermedadCardiovascular-1)
		(atributo afecta)
		(valor vasosSanguineos))
	(oav-u  (objeto enfermedadCardiovascular-1)
		(atributo enfermedadCardiovascular)
		(valor true))
	(oav-u  (objeto enfermedadCardiovascular-2)
		(atributo nombre)
		(valor estenosisArterial))
	(oav-u  (objeto enfermedadCardiovascular-2)
		(atributo afecta)
		(valor vasosSanguineos))
	(oav-u  (objeto enfermedadCardiovascular-2)
		(atributo enfermedadCardiovascular)
		(valor true))
	(oav-u  (objeto enfermedadCardiovascular-3)
		(atributo nombre)
		(valor arteriosclerosis))
	(oav-u  (objeto enfermedadCardiovascular-3)
		(atributo afecta)
		(valor vasosSanguineos))
	(oav-u  (objeto enfermedadCardiovascular-3)
		(atributo enfermedadCardiovascular)
		(valor true))
	(oav-u  (objeto enfermedadCardiovascular-4)
		(atributo nombre)
		(valor regurgitaciÃ³nAortica))
	(oav-u  (objeto enfermedadCardiovascular-4)
		(atributo afecta)
		(valor corazon))
	(oav-u  (objeto enfermedadCardiovascular-4)
		(atributo enfermedadCardiovascular)
		(valor true))
		
)
(defrule aneurismaAortica
	(oav-u  (objeto ?x)
		(atributo paciente)
		(valor true))
	(oav-m (objeto ?x)
		(atributo sintomas)
		(valor dolorAbdominal))
	(oav-m (objeto ?x)
		(atributo evidenciaClinica)
		(valor rumorAbdominal))
	(oav-m (objeto ?x)
		(atributo evidenciaClinica)
		(valor masaPulsante))
	=>
	(assert(oav-m (objeto ?x)
		(atributo enfermedadCardiovascular)
		(valor aneurismaAortico)))
)
	
(defrule regurgitacionAortica
	(oav-u  (objeto ?x)
		(atributo paciente)
		(valor true))
	(oav-u (objeto ?x)
		(atributo presionSistolica)
		(valor ?y))
	(oav-u (objeto ?x)
		(atributo presionDiastolica)
		(valor ?z))
	(test(> ?y 130))
	(test(> (- ?y ?z) 50))
	(or 	(oav-m (objeto ?x)
			(atributo evidenciaClinica)
			(valor rumorSistolico))
		(oav-m (objeto ?x)
			(atributo evidenciaClinica)
			(valor dilatacionCorazon)))
	
	=>
	(assert(oav-m (objeto ?x)
		(atributo enfermedadCardiovascular)
		(valor regurgitacionAortica)))
)

(defrule estenosisArterial 
	(oav-u  (objeto ?x)
		(atributo paciente)
		(valor true))
	(oav-m (objeto ?x)
		(atributo sintomas)
		(valor calambresPiernas))
	=>
	(assert(oav-m (objeto ?x)
		(atributo enfermedadCardiovascular)
		(valor estenosisArterial)))
)

(defrule grupoDeRiesgo
	(oav-u  (objeto ?x)
		(atributo paciente)
		(valor true))
	(oav-u (objeto ?x) 
		(atributo obeso) 
		(valor ?obeso))
	(oav-u (objeto ?x) 
		(atributo anosFumando) 
		(valor ?anos))
	(oav-u (objeto ?x) 
		(atributo edad) 
		(valor ?edad))
	
	(test (or 
		(eq ?obeso true) 
		(> ?anos 10) 
		(> ?edad 55)))
	
	=>
	(assert (oav-u (objeto ?x)
		(atributo grupoDeRiesgo)
		(valor true))))



(defrule arterioesclerosis 
	(oav-u  (objeto ?x)
		(atributo paciente)
		(valor true))
	(oav-m (objeto ?x)
		(atributo enfermedadCardiovascular)
		(valor estenosisArterial))
	(oav-u (objeto ?x)
		(atributo grupoDeRiesgo)
		(valor true))
	=>
	(assert(oav-m (objeto ?x)
		(atributo enfermedadCardiovascular)
		(valor arteriosclerosis)))
)	

(defrule resultados
	(declare(salience -10))
	(oav-u  (objeto ?x)
		(atributo paciente)
		(valor true))
	(oav-m (objeto ?x)
		(atributo enfermedadCardiovascular)
		(valor ?y))
	
	(oav-u (objeto ?x)
		(atributo nombre)
		(valor ?z))
	
	(oav-u  (objeto ?q)
		(atributo enfermedadCardiovascular)
		(valor true))
		
	(oav-u (objeto ?q)
		(atributo nombre)
		(valor ?y))
		
	(oav-u (objeto ?q)
		(atributo afecta)
		(valor ?t))
		
	=>
	(printout t "El "?x", de nombre " ?z " sufre " ?y " una enfermedad de tipo cardiovascular que afecta a la siguiente zona: " ?t "."crlf)
	
)

(defrule sinEnfermedades
	(declare (salience -10))
	(oav-u  (objeto ?x)
		(atributo paciente)
		(valor true))
	(oav-u (objeto ?x) 
	       (atributo nombre) 
	       (valor ?z))
	(not (oav-m (objeto ?x)
		(atributo enfermedadCardiovascular)
		(valor ?)))
	=>
	(printout t "Al " ?x ", de nombre " ?z " no se le ha asignado ningun diagnóstico de enfermedad cardiovascular." crlf)
)


